model {
   alpha0  ~ dnorm(0.0,1.0E-6);  # intercept
   alpha.urban  ~ dnorm(0.0,1.0E-6);
   alpha.age  ~ dnorm(0.0,1.0E-6);
   alpha.ch1 ~ dnorm(0.0,1.0E-6);
   alpha.ch2 ~ dnorm(0.0,1.0E-6);
   alpha.ch3 ~ dnorm(0.0,1.0E-6);

   tau.i ~  dgamma(1.0E-3,1.0E-3);
   sigma.i <- 1.0/sqrt(tau.i);

   tau.u ~  dgamma(1.0E-3,1.0E-3);
   sigma.u <- 1.0/sqrt(tau.u);

  for (j in 1:M){
     b.i[j]  ~ dnorm(0.0,tau.i);
     b.u[j]  ~ dnorm(0.0,tau.u); ## independent variance
  }

   for (i in 1:N) {
      logit(p[i]) <- alpha0 + alpha.age*age[i] + alpha.urban*urbanBin[i] +
                     alpha.ch1*ch1[i] + alpha.ch2*ch2[i] + alpha.ch3*ch3[i] +
                     b.u[district2[i]]*urbanBin[i] + b.i[district2[i]];
      useBin[i] ~ dbern(p[i]);  
   }
}
